MEMORY
{
  RAM (rwx) : ORIGIN = 0x00100000, LENGTH = 0x28000
  EM (rwx) : ORIGIN = 0x00129dc6, LENGTH = 0xa23A
}

ENTRY(Reset_Handler)

SECTIONS
{
    
    RAM_CODE :  
    { 
        KEEP(*(.isr_vector))
        KEEP(*(jump_table_area))
        *(.init)
        *(.fini)       
        *(.text*)
        *(.rodata*)
        *(HARDFAULT_CODE)
        *(.eh_frame*)
        *(.data*) 
    }> RAM
    .copy.table :
    {
        . = ALIGN(4);
        __copy_table_start__ = .;
        __copy_table_end__ = .;
    }> RAM
    .zero.table :
    {
        . = ALIGN(4);
        __zero_table_start__ = .;
        LONG (__bss_start__)
        LONG (__bss_end__ - __bss_start__)
        __zero_table_end__ = .;
    }> RAM
    RWIP_ENV (NOLOAD):
    {
        *rwip*(.bss* COMMON)
    }> EM
    .bss (NOLOAD):
    {
        __bss_start__ = .;
        *(.bss*) 
        *(SECTION_RTT)
        *(COMMON)
        __bss_end__ = .;
    }> EM
    .heap (NOLOAD):
    {
        __end__ = .;
        PROVIDE(end = .);
        *(.heap*)
        __HeapLimit = .;
    }> EM
    .stack_dummy (NOLOAD):
    {
        *(.stack*)

    }> EM

    BOOT_PARAMS (NOLOAD): {*(boot_tunnel)}> EM
    RAM_UNLOADED (NOLOAD):
    { 
        . = ALIGN(4); 
        unloaded_area_start = .;
        . += 12;
        unloaded_area_start_end = .;
    }> EM

    NVDS_AREA 0xb0000000 :
    {
        KEEP(*(nvds_area))
    }
}
